configurations {
    driver
}

buildscript {
    ext {
        springBootVersion = '1.5.3.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.flywaydb:flyway-gradle-plugin:3.2.1")
        classpath("org.postgresql:postgresql:42.1.1")
        classpath("net.saliman:gradle-cobertura-plugin:2.3.1")
    }
}


group 'college-app-tracker'

apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'idea'
apply plugin: 'org.flywaydb.flyway'
apply plugin: 'application'
apply plugin: 'cobertura'

mainClassName = "com.erwindev.apptracker.Application"

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    baseName = 'college-app-tracker'
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.11'

    compile("org.springframework.boot:spring-boot-starter-jdbc:${springBootVersion}"){
        exclude module: 'tomcat-jdbc'
    }
    compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}"){
        exclude module: 'spring-boot-starter-tomcat'
    }
    //use jetty
    compile("org.springframework.boot:spring-boot-starter-jetty:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-security:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}")

    compile("org.postgresql:postgresql:42.1.1")
    compile("com.h2database:h2:1.4.196")

    compile("com.zaxxer:HikariCP:2.6.3")

    compile("org.flywaydb:flyway-core:4.2.0")

    compile("io.springfox:springfox-swagger2:2.7.0")
    compile("io.springfox:springfox-swagger-ui:2.7.0")

    compile("io.jsonwebtoken:jjwt:0.7.0")

    testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")

    driver("org.postgresql:postgresql:42.1.1")
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4'
}

URLClassLoader loader = GroovyObject.class.classLoader as URLClassLoader
configurations.driver.each {
    File file -> loader.addURL(file.toURI().toURL())
}
loader.loadClass('org.postgresql.Driver')

import groovy.sql.Sql

task initdb {
    doLast {
        def driver = 'org.postgresql.Driver'
        def user = System.getenv("PG_USER")
        def password = System.getenv("PG_PASSWORD")
        def url = System.getenv("PG_URL")

        try {
            Sql sql = Sql.newInstance(url, user, password, driver)
            sql.execute "REVOKE CONNECT ON DATABASE college FROM public"
            sql.execute "SELECT pg_terminate_backend(pg_stat_activity.pid)\n" +
                    "FROM pg_stat_activity\n" +
                    "WHERE pg_stat_activity.datname = 'college';"
            sql.execute "drop database if exists \"college\""
            sql.execute "create database \"college\" with encoding = 'UTF8'"
            sql.execute "drop user if exists tracker_user"
            sql.execute "create user tracker_user with password 'tr@ck3r_u53r'"
            sql.execute "grant all privileges on schema public to tracker_user"

        }
        catch(Exception e){
            println(e)
        }

    }
}

task initSchema{
    doLast {
        def driver = 'org.postgresql.Driver'
        def user = System.getenv("SPRING_DATASOURCE_USERNAME")
        def password = System.getenv("SPRING_DATASOURCE_PASSWORD")
        def url = System.getenv("SPRING_DATASOURCE_URL")

        try {
            Sql sql = Sql.newInstance(url, user, password, driver)
            sql.execute "grant all privileges on schema public to tracker_user"
        }
        catch(Exception e){
            println(e)
        }
    }
}

flyway {
    user = System.getenv("SPRING_DATASOURCE_USERNAME")
    password = System.getenv("SPRING_DATASOURCE_PASSWORD")
    url = System.getenv("SPRING_DATASOURCE_URL")
}

cobertura {
    coverageFormats = ['html', 'xml']
    coverageIgnoreTrivial = true
    coverageIgnores = ['org.slf4j.Logger.*']
    coverageReportDir = new File("$buildDir/reports/cobertura")
}